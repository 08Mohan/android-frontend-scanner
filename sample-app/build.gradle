apply plugin: 'com.android.application'

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.sensorberg.android.sensorscanner.sample"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode System.getenv("BUILD_NUMBER") as Integer ?: 9999
        versionName "1.0"
    }
    buildTypes {
        release {         
        }
    }

    def isReleaseAble = System.getenv("SENSORBERG_KEYSTORE_PATH") != null

    signingConfigs {
        if (isReleaseAble) {
            release {
                storeFile file(System.getenv("SENSORBERG_KEYSTORE_PATH"))
                storePassword System.getenv("SENSORBERG_KEYSTORE_PASSWORD")
                keyAlias System.getenv("SENSORBERG_KEYSTORE_KEY_ALIAS")
                keyPassword System.getenv("SENSORBERG_KEYSTORE_PASSWORD")
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            applicationIdSuffix ".release"
            if (isReleaseAble) {
                signingConfig signingConfigs.release
            }
        }


        playStore.initWith(release)
        playStore{
            applicationIdSuffix null
        }
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

def output = new File("build/outputs/screenshot")

task cleanRobotiumScreenshots <<{
    deleteDir(output)
}

clean.dependsOn(cleanRobotiumScreenshots)

task pullRobotiumScreenshots << {

//    doFirst {
//        if (!output.exists()){
//            println "creating ${output.absolutePath} folder"
//            output.mkdirs();
//        }
//    }
    exec {
        commandLine "/usr/local/bin/adb", "pull", "/sdcard/Robotium-Screenshots/", output.getAbsolutePath()
    }
}
pullRobotiumScreenshots.shouldRunAfter(cleanRobotiumScreenshots)

dependencies {    
    compile 'com.android.support:support-v13:21.0.0'
    compile 'com.android.support:appcompat-v7:21.0.+'

    compile project(':sensorscanner')

    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.2.1'
}

boolean deleteDir(File dir) {
    if (dir.isDirectory()) {
        for (String child : dir.list()) {
            if (!deleteDir(new File(dir, child))) {
                return false;
            }
        }
    }
    return dir.delete();
}
